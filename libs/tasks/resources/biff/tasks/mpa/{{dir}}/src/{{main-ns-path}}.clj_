(ns {{main-ns}}
  (:require
    [biff.auth :as auth]
    [biff.components :as c]
    [biff.core :as biff]
    [biff.util-tmp :as bu]
    [biff.components-tmp :as bcomp]
    [biff.crux-tmp :as bcrux]
    [biff.middleware :as bmid]
    [reitit.ring :as reitit]
    [ring.middleware.session.cookie :as cookie]
    [{{parent-ns}}.config :refer [merge-config]]
    [{{parent-ns}}.email :refer [send-email]]
    [{{parent-ns}}.jobs :refer [jobs]]
    [{{parent-ns}}.routes :refer [routes]]
    [{{parent-ns}}.rules :refer [rules]]
    [{{parent-ns}}.static :refer [pages]])
  (:gen-class))

(defn error [{:keys [which]}]
  (let [[status msg] (case which
                       :not-found [404 "Not found."]
                       :method-not-allowed [405 "Not allowed."]
                       :not-acceptable [406 "Not acceptable."])]
    {:status status
     :headers {"Content-Type" "text/plain"}
     :body msg}))

(defn default-handlers [{:keys [error]}]
  [(reitit/create-resource-handler
     {:path "/"})
   (reitit/create-default-handler
     (->> [:not-found :method-not-allowed :not-acceptable]
          (map (fn [k]
                 [k #(error (assoc % :which k))]))
          (into {})))])

(defn wrap-print-requests [handler]
  (fn [req]
    (let [resp (handler req)]
      (println (:status resp) (:request-method req) (:uri req))
      resp)))

(defn wrap-biff-crux [handler]
  (fn [req]
    (handler (assoc req :biff/db @(:biff.crux/db req)))))

(defn wrap-middleware [{:keys [biff.crux/node
                               biff.web/cookie-secret
                               biff.web/secure-cookies
                               biff.web/handler
                               biff.web/on-error]
                        :or {on-error (constantly
                                        {:status 500
                                         :headers {"Content-Type" "text/plain"}
                                         :body "Internal error."})}
                        :as sys}]
  (let [store (cookie/cookie-store
                {:key (bu/base64-decode cookie-secret)})]
    (assoc sys :biff.web/handler
           (-> handler
               wrap-biff-crux
               (bcrux/wrap-db {:node node})
               (bmid/wrap-defaults {:session-store store
                                    :secure secure-cookies
                                    :env sys
                                    :on-error on-error})
               wrap-print-requests))))

(def components
  [merge-config
   c/set-defaults
   #(dissoc % :biff.http/spa-path)
   c/start-crux
   (fn [sys]
     (merge sys
            {:biff.crux/node (:biff/node sys)}))
   bcomp/reitit
   (fn [sys]
     (update sys :biff.reitit/routes conj (auth/route sys)))
   wrap-middleware
   bcomp/jetty
   c/start-jobs])

(defn -main []
  (bu/start-system
    {:biff.reitit/routes routes
     :biff.reitit/default-handlers (default-handlers {:error error})
     :biff/static-pages pages
     :biff/rules #'rules
     :biff/jobs jobs
     :biff/send-email #'send-email
     :biff/after-refresh `-main}
    components)
  (println "System started."))
